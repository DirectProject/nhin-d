 
; Copyright (c) 2010, Direct Project
; All rights reserved.
;
; Authors:
;    Joseph Shook    JoeShook@Gmail.com
;   
; 
;Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
;
;Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
;Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
;Neither the name of The Direct Project (directproject.org) nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
;THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 


; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include "InnoScripts\IISUtils.iss"
#include "InnoScripts\VcRuntimeInstalled.iss"
#include "InnoScripts\WindowsServicesUtils.iss"
#include "InnoScripts\GetCommandLineParams.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
ArchitecturesInstallIn64BitMode=x64 ia64
AppId={{995D337A-5620-4537-9704-4B19EC628A39}
AppName=Direct Project .NET Gateway
AppVerName=Direct Project .NET Gateway 1.0.0.0
AppPublisher=The Direct Project (nhindirect.org)
AppPublisherURL=http://nhindirect.org
AppSupportURL=http://nhindirect.org
AppUpdatesURL=http://nhindirect.org
DefaultDirName={pf}\Direct Project .NET Gateway
DefaultGroupName=Direct Project .NET Gateway
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename=Direct-1.0.0.0-NET35
Compression=lzma
SolidCompression=yes
VersionInfoVersion=1.0.0.0
SetupLogging=yes
PrivilegesRequired=admin

WizardImageFile=Direct.bmp
WizardSmallImageFile=DirectSmall.bmp
WizardImageStretch=Yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Components]
Name: dnsresponder; Description: Install the DNS Responder; Types: development dns; 
Name: dnswebservice; Description: DNS Web service; Types: development config; 
Name: configwebservice; Description: Config Web services; Types: development config; 
Name: configui; Description: UI Web Admin; Types: development config;
Name: directgateway; Description: Gateway to SMTP; Types: development gateway; 
Name: developergateway; Description: Developer gateway configuration to SMTP; Types: development; 
Name: database; Description: DirectConfig database; Types: development database;

     
[Types]
Name: gateway; Description: Gateway               
Name: dns; Description: DNS Responder; 
Name: config; Description: Config services             
Name: database; Description: Database
Name: custom; Description: Custom Install; Flags: iscustom;
Name: development; Description: Developer Install (Single machine and development gateway version)


[Files]
Source: "..\bin\debug\*.dll"; DestDir: "{app}"; Flags: ignoreversion;  Components: dnsresponder dnswebservice configwebservice configui directgateway developergateway; 
Source: "..\bin\debug\Win32\smtpEventHandler.dll"; DestDir: "{app}"; Flags: ignoreversion; Check: IsX86;  Components: dnsresponder dnswebservice configwebservice configui directgateway developergateway; 
Source: "..\bin\debug\x64\smtpEventHandler.dll"; DestDir: "{app}"; Flags: ignoreversion; Check: IsX64 or IsIA64; Components: dnsresponder dnswebservice configwebservice configui directgateway developergateway;                            
Source: "..\bin\debug\*.config"; DestDir: "{app}"; Excludes: "*.vshost.*,*.dll.config"; Flags: ignoreversion; Components: dnsresponder dnswebservice configwebservice configui directgateway developergateway; 
Source: "..\bin\debug\*.exe"; DestDir: "{app}"; Excludes: "*.vshost.*"; Flags: ignoreversion; Components: dnsresponder dnswebservice configwebservice configui directgateway developergateway; 
Source: "..\bin\debug\Certificates\*"; DestDir: "{app}\Certificates"; Flags: ignoreversion recursesubdirs;   Components: developergateway; 
Source: "..\bin\debug\ConfigConsoleSettings.xml"; DestDir: "{app}"; Flags: ignoreversion; Components: developergateway

Source: "..\config\service\*.svc"; DestDir: "{app}\ConfigService"; Flags: ignoreversion; Components: configwebservice developergateway; 
Source: "..\config\service\*.aspx"; DestDir: "{app}\ConfigService"; Flags: ignoreversion; Components: configwebservice developergateway; 
Source: "..\config\service\*.config"; DestDir: "{app}\ConfigService"; Flags: ignoreversion; Components: configwebservice developergateway; 
Source: "..\config\service\bin\*.dll"; DestDir: "{app}\ConfigService\bin"; Flags: ignoreversion recursesubdirs; Components: configwebservice developergateway; 

Source: "..\dnsresponder.service\*.svc"; DestDir: "{app}\DnsService"; Flags: ignoreversion; Components: dnswebservice developergateway; 
Source: "..\dnsresponder.service\*.aspx"; DestDir: "{app}\DnsService"; Flags: ignoreversion; Components: dnswebservice developergateway; 
Source: "..\dnsresponder.service\*.config"; DestDir: "{app}\DnsService"; Flags: ignoreversion; Components: dnswebservice developergateway; 
Source: "..\dnsresponder.service\bin\*.dll"; DestDir: "{app}\DnsService\bin"; Flags: ignoreversion recursesubdirs; Components: dnswebservice developergateway; 

Source: "..\installer\configui\*"; DestDir: "{app}\ConfigUI"; Flags: ignoreversion recursesubdirs; Components: configui developergateway;
Source: "..\installer\configui\config\dev.client.config"; DestDir: "{app}\ConfigUI\Config";  DestName: "client.config";  Flags: ignoreversion; Components: configui and not developergateway

Source: "..\gateway\install\*.vbs"; DestDir: "{app}"; Flags: ignoreversion; Components: directgateway;
Source: "..\gateway\install\*.bat"; DestDir: "{app}"; Excludes: "backup.bat,copybins.bat"; Flags: ignoreversion; Components: directgateway;
Source: "SmtpAgentConfig.xml"; DestDir: {app}; Flags: ignoreversion; Components: directgateway;

Source: "..\gateway\devInstall\DevAgentWithServiceConfig.xml"; DestDir: "{app}"; DestName: "DevAgentConfig.xml"; Flags: ignoreversion; Components: developergateway;            
Source: "..\gateway\devInstall\setupdomains.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: developergateway;
Source: "..\gateway\devInstall\simple.eml"; DestDir: "{app}\Samples"; Flags: ignoreversion; Components: developergateway;

Source: "..\external\microsoft\vcredist\vcredist_x86.exe"; DestDir: "{app}\Libraries"; DestName: "vcredist.exe"; Flags: ignoreversion recursesubdirs; Check: IsX86; Components: directgateway developergateway;
Source: "..\external\microsoft\vcredist\vcredist_x64.exe"; DestDir: "{app}\Libraries"; DestName: "vcredist.exe"; Flags: ignoreversion recursesubdirs; Check: IsX64 or IsIA64; Components: directgateway developergateway;

Source: "*.bat"; DestDir: "{app}"; Excludes: "build-installer.bat"; Flags: ignoreversion;
Source: "*.ps1"; DestDir: "{app}"; Flags: ignoreversion;
Source: "event-sources.txt"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\config\store\Schema.sql"; DestDir: "{app}\SQL"; Flags: ignoreversion; Components: database developergateway; 
Source: "createuser.sql"; DestDir: "{app}\SQL"; Flags: ignoreversion; Components: database developergateway; 
                        
Source: "toolutil\install.tools\bin\debug\Health.Direct.Install.Tools.dll"; DestDir: "{app}\InstallTools"; Flags: ignoreversion;  

                                 
[UninstallDelete]
Type: files; Name: "{app}\direct.ini"
Type: files; Name: "{app}\Health.Direct.SmtpAgent.tlb"
Type: files; Name: "{app}\InstallationLogFile.log"
Type: files; Name: "{app}\installdnsresponder.log"
Type: files; Name: "{app}\installgateway.log"
Type: files; Name: "{app}\createeventlogsource.log"
Type: files; Name: "{app}\InstallTools\Health.Direct.Install.Tools.tlb"

[Icons]
Name: "{group}\Admin Console"; Filename: "{app}\AdminConsole.exe"; WorkingDir: "{app}";
Name: "{group}\Config Console"; Filename: "{app}\ConfigConsole.exe"; WorkingDir: "{app}";
Name: "{group}\Config Web UI"; Filename: "http://localhost/ConfigUI/";
Name: "{group}\Test Database"; Filename: "http://localhost/ConfigService/TestService.aspx";
Name: "{group}\{cm:UninstallProgram,Direct Gateway}"; Filename: "{uninstallexe}";


[Run]
Filename: {app}\Libraries\vcredist.exe; Description: "Microsoft Visual C++ 2008 Redistributable Package"; Flags: postinstall runascurrentuser unchecked; Components: directgateway or developergateway; Check: not IsVCRT
Filename: {app}\createdatabase.bat; Parameters: ".\sqlexpress DirectConfig ""{app}\SQL\Schema.sql"" ""{app}\SQL\createuser.sql"""; Description: Install Database; Flags: runascurrentuser postinstall; Components: developergateway and not database;
Filename: {app}\createdatabase.bat; Parameters: ".\sqlexpress DirectConfig ""{app}\SQL\Schema.sql"" ""{app}\SQL\createuser.sql"""; Description: Install Database; Flags: runascurrentuser; Components: database;
Filename: {app}\install-dev.bat; Parameters: """{app}"""; Description: "Install Gateway (DEVELOPMENT VERSION)"; WorkingDir: "{app}"; Flags: postinstall runascurrentuser unchecked; Components: developergateway;
Filename: {app}\installdnsresponder.bat; Parameters: """{app}"" >> ""{app}\installdnsresponder.log"" 2>&1"; Description: Install DNS Responder; Flags: runascurrentuser ; Components: dnsresponder and not developergateway; 
Filename: {dotnet2032}\RegAsm.exe; Parameters: Health.Direct.Install.Tools.dll /codebase; WorkingDir:{app}\InstallTools; StatusMsg: Installing installer tools; Description: Register tool com visible; Flags: runascurrentuser; 
Filename: {dotnet2064}\RegAsm.exe; Parameters: Health.Direct.Install.Tools.dll /codebase; WorkingDir:{app}\InstallTools; StatusMsg: Installing installer tools; Description: Register tool com visible; Flags: runascurrentuser; 
Filename: {app}\installgateway.bat; Parameters:  """{app}"" >> ""{app}\installgateway.log"" 2>&1";  Description: Install Gateway; Flags: runascurrentuser ; Components: directgateway and not developergateway; 
Filename: {app}\createadmin.bat; Description:Create Admin.  (Database must exist); Flags: runascurrentuser postinstall unchecked; Components: not developergateway; 
Filename: {app}\createeventlogsource.bat; Parameters: " >> ""{app}\createeventlogsource.log"" 2>&1"; Description:Setup event log; Flags: runascurrentuser; Components: (developergateway or dnsresponder or dnswebservice or configwebservice) and not developergateway; 


[UninstallRun]
Filename: {app}\uninstall.bat; Flags: runascurrentuser; RunOnceId: 'RemoveDeveloperGateway';   Components: developergateway;
Filename: {app}\uninstallDnsResponder.bat; RunOnceId: 'RemoveDnsResponder';  Components: dnsresponder and not developergateway;
Filename: {app}\uninstallGateway.bat; RunOnceId: 'RemoveGateway'; Components: directgateway and not developergateway;
Filename: {dotnet2064}\RegAsm.exe;  Parameters: Health.Direct.Install.Tools.dll /unregister; WorkingDir:{app}\InstallTools; Flags: runascurrentuser; Components: dnsresponder and not developergateway;
Filename: {dotnet2032}\RegAsm.exe;  Parameters: Health.Direct.Install.Tools.dll /unregister; WorkingDir:{app}\InstallTools; Flags: runascurrentuser; Components: dnsresponder and not developergateway;


[INI]
Filename: {app}\direct.ini; section: InstallSettings; key: "DnsWebService_Vdir"; string: DnsService; Components: dnswebservice
Filename: {app}\direct.ini; section: InstallSettings; key: "ConfigWebService_Vdir"; string: ConfigService; Components: configwebservice
Filename: {app}\direct.ini; section: InstallSettings; key: ConfigUiWebApp_Vdir; string: ConfigUI; Components: configui



[Code]



var
  //Log file maintenance
  OkToCopyLog : Boolean;
  toolsRegistered : Boolean; //Flag to indicate tools have been registered in the temp folder.
  
  //Global dns variable
  StartDnsServicePostProcessing : Boolean;
  strDnsServiceNameToCheck : String;

   


function IsX64: Boolean;
begin
  Result := Is64BitInstallMode and (ProcessorArchitecture = paX64);
end;

function IsIA64: Boolean;
begin
  Result := Is64BitInstallMode and (ProcessorArchitecture = paIA64);
end;

function IsX86: Boolean;
begin
  Result := (ProcessorArchitecture = paX86);
end;

//included script to test for VC redistributable
function IsVCRT: Boolean;                                                                                        
begin
  Result := VCRT_IsInstalled (VC2008_ANY_x64) = 5;
end;




procedure DnsServiceStop();
  var        
    status: Boolean;       
begin
  strDnsServiceNameToCheck := 'DirectDnsResponderSvc';

  if IsServiceRunning(strDnsServiceNameToCheck) then begin        
    status := StopService(strDnsServiceNameToCheck);
    StartDnsServicePostProcessing := true;
    Sleep(2000);
    if not status then
      MsgBox('Stop the Direct Dns Service manually to continue the installation', mbError, mb_Ok);     
  end;
end;





function InitializeSetup(): Boolean; 
begin
  DnsServiceStop();       
  Result := true;      
end;


procedure DeinitializeSetup();
begin
    if StartDnsServicePostProcessing then begin
      if not IsServiceRunning(strDnsServiceNameToCheck) then begin
        StartService(strDnsServiceNameToCheck);
      end;
    end;

    //log files processing
    if OkToCopyLog then
    FileCopy (ExpandConstant ('{log}'), ExpandConstant ('{app}\InstallationLogFile.log'), FALSE);
    RestartReplace (ExpandConstant ('{log}'), '');   // remove the temp log file during the next system restart.


end;


function PrepareToInstall(var NeedsRestart: Boolean): String;
var
  ResultCode : Integer; 
begin
  Exec('IISRESET','', '', SW_SHOW, ewWaitUntilTerminated, ResultCode );    
  Result := '';   
end;



function TestConnection(endpoint : String): Boolean;
var
  tools: Variant; 
  success: Boolean;
begin
  success := false;
  try                              
    tools := CreateOleObject('Direct.Installer.EndPointTools');
  except
    RaiseException('Cannot find Direct.Installer.EndPointTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    try
      success := tools.TestWcfSoapConnection(endpoint);
    except
      RaiseException('Failed end point test for endpoint: ' + endpoint +  #13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
    end;
    Result := success;
end;



procedure RunTestConnect(wizPage: TWizardPage; labelName, textboxName : String );
var
  myTextBox : TCustomEdit;
  myLabel : TNewStaticText;  
begin   
  myTextBox := TCustomEdit(wizPage.FindComponent(textboxName));
  myLabel :=  TNewStaticText(wizPage.FindComponent(labelName));       

  if(TestConnection(myTextBox.Text)) then
  begin    
    myLabel.Font.Color := clGreen;
  end else
  begin
    myLabel.Font.Color := clRed;
  end;
  myLabel.Update;     
end;



procedure RunTestServiceAspx(wizPage: TWizardPage; labelName, textboxName : String );
var
  tools : Variant;
  myTextBox : TCustomEdit;
  myLabel : TNewStaticText;
  success : Boolean;  
begin   
  myTextBox := TCustomEdit(wizPage.FindComponent(textboxName));
  myLabel :=  TNewStaticText(wizPage.FindComponent(labelName));       


  success := false;
  try                              
    tools := CreateOleObject('Direct.Installer.EndPointTools');
  except
    RaiseException('Cannot find Direct.Installer.EndPointTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;

  try
    success := tools.TestConnection(myTextBox.Text, 'Database is accessible!');
  except
    RaiseException('Failed end point test for endpoint: ' + myTextBox.Text +  #13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    
  if(success) then
  begin    
    myLabel.Font.Color := clGreen;
  end else
  begin
    myLabel.Font.Color := clRed;
  end;
  myLabel.Update;     
end;


procedure CheckDnsResponderServiceOnClick(Sender: TObject);
var            
  EndPointsButton : TNewButton;
  DnsResponderPage : TWizardPage;
  buttonCaption : String;   
begin
  EndPointsButton := TNewButton(Sender);
  DnsResponderPage := TWizardPage(EndPointsButton.Owner);   
  
  buttonCaption := EndPointsButton.Caption;
  EndPointsButton.Caption := 'Checking...';
  //ShellExecAsOriginalUser('open', DnsServiceTestTextBox.Text, '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode); 
  try      
    RunTestServiceAspx(DnsResponderPage, 'DnsServiceTestLabel', 'DnsServiceTestTextBox');      
    RunTestConnect(DnsResponderPage, 'DnsServiceLabel', 'DnsServiceUrlText');          
  except         
    RaiseException(GetExceptionMessage);
  finally
    EndPointsButton.Caption := buttonCaption;
    EndPointsButton.Update;  
  end;
end;




procedure CheckDatabaseConnOnClick(Sender: TObject);
var
  tools: Variant;
  Button: TButton;
  DbConnStrTextBox: TNewEdit;
  DatabaseConnPage: TWizardPage;  
  StatusLabel : TNewStaticText;
  success : Boolean;
  message : String;
begin
  Button := TButton(Sender);
  DatabaseConnPage := TWizardPage(Button.Owner);
  DbConnStrTextBox := TNewEdit(DatabaseConnPage.FindComponent('DbConnStrTextBox'));   
  StatusLabel := TNewStaticText(DatabaseConnPage.FindComponent('StatusLabel'));
  StatusLabel.Font.Color := clBlack;
  StatusLabel.Caption := 'Checking Db Connection...';
  StatusLabel.Update;
  try
    tools := CreateOleObject('Direct.Installer.SqlDbTools');
  except
    RaiseException('Cannot find Direct.Installer.SqlDbTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    success := tools.TestConnection(DbConnStrTextBox.Text, message);     
    if(success) then
    begin
      StatusLabel.Font.Color := clGreen;
      StatusLabel.Caption := 'Success';      
      StatusLabel.Update;
    end
    else
    begin
      StatusLabel.Font.Color := clRed;
      StatusLabel.Caption := 'Failed: ' + message;           
      StatusLabel.Update;
    end;         
end;


procedure SetDnsResponderUrl(url: String);
var
  tools: Variant;
begin
  try                              
    tools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    tools.XmlFilePath := ExpandConstant('{app}') + '\DirectDnsResponderSvc.exe.config' ;
    tools.SetSingleAttribute('/configuration/ServiceSettingsGroup/RecordRetrievalServiceSettings/@Url', url);
end;






procedure SetDatabaseConnSting(connStr: String);
var
  tools : Variant;
begin
  try
    tools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    if (pos( 'dnswebservice', WizardSelectedComponents( false)) > 0) and (pos( 'development', WizardSetupType( false)) = 0) then  
    begin
        tools.XmlFilePath := ExpandConstant('{app}') + '\DnsService\Web.Config' ;
        tools.SetSingleAttribute('configuration/connectionStrings/add[@name="configStore"]/@connectionString', connStr);
    end
    else
    if (pos( 'configwebservice', WizardSelectedComponents( false)) > 0)  and (pos( 'development', WizardSetupType( false)) = 0) then  
    begin
        tools.XmlFilePath := ExpandConstant('{app}') + '\ConfigService\Web.Config' ;
        tools.SetSingleAttribute('configuration/connectionStrings/add[@name="configStore"]/@connectionString', connStr);
    end;
end;
       

       



function SetDnsResponderUrlOnClick(Sender: TWizardPage): Boolean;
var
  DnsServiceUrlText : TNewEdit;
begin         
    DnsServiceUrlText := TNewEdit(Sender.FindComponent('DnsServiceUrlText'));
    SetDnsResponderUrl(DnsServiceUrlText.Text);
    Result := True;
end;


//Save connection string
//Determine which config files to save it to.
function SetDatabaseConnUrlOnClick(Sender: TWizardPage): Boolean;
var
  DbConnStrTextBox : TNewEdit;
begin  
  DbConnStrTextBox := TNewEdit(Sender.FindComponent('DbConnStrTextBox')); 
  SetDatabaseConnSting(DbConnStrTextBox.Text);
  Result := True; 
end;


procedure WriteConfigItem(wizardPage : TWizardPage; configFile, xpath, objectName : String);
var
  xpathTools: Variant;     
  textBox: TCustomEdit; 
  labelText: TNewStaticText;
  value : String; 
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    textBox := TCustomEdit(wizardPage.FindComponent(objectName));   
    labelText := TNewStaticText(wizardPage.FindComponent(objectName));   
    if not (textBox = nil) then
    begin
      value := textBox.text;
    end
    else
    begin
      value := labelText.Caption;
    end;
       
    xpathTools.XmlFilePath := configFile;
    xpathTools.SetSingleAttribute(xpath, value);  
    
end;



//Configure Config Admin client endpoint urls.
function SetConfigAdminEndpointsOnClick(Sender: TWizardPage): Boolean;
var
  configFile  : String;
begin
  configFile := ExpandConstant('{app}') + '\ConfigUI\Config\client.config';      
  
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_ICertificateStore"]/@address', 'CertificatesUrlText'); 
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_IAnchorStore"]/@address', 'AnchorsUrlText'); 
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_IAddressManager"]/@address', 'AddressesUrlText'); 
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_IDomainManager"]/@address', 'DomainsUrlText'); 
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_IDnsRecordManager"]/@address', 'DnsRecordsUrlText'); 
  WriteConfigItem(Sender, configFile, 'client/endpoint[@bindingConfiguration="BasicHttpBinding_IAuthManager"]/@address', 'AuthenticationUrlText'); 
 
  Result := True; 
end;

   
function SetGatewayConfigOnClick(Sender: TWizardPage): Boolean;
var                
  configFile  : String;
begin
  
  configFile := ExpandConstant('{app}') + '\SmtpAgentConfig.xml';
  
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/Domain', 'DomainText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/DomainManager/Url', 'DomainManagerText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/AddressManager/Url', 'AddressManagerText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/PrivateCerts/ServiceResolver/ClientSettings/Url', 'PrivateCertsText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/Anchors/ServiceResolver/ClientSettings/Url', 'AnchorsText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/PublicCerts/DnsResolver/ServerIP', 'DnsResolverIpText');
  
  Result := True;
end;

function SetConfigSmtpAdminEndpointsOnClick(Sender: TWizardPage): Boolean;
var                
  configFile  : String;
begin
  
  configFile := ExpandConstant('{app}') + '\SmtpAgentConfig.xml';
  
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/InternalMessage/PickupFolder', 'PickupText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/RawMessage/CopyFolder', 'RawMessageText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/BadMessage/CopyFolder', 'BadMessageText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/ProcessIncoming/CopyFolder', 'IncommingMessageText');
  WriteConfigItem(Sender, configFile, '/SmtpAgentConfig/ProcessOutgoing/CopyFolder', 'OutgoingMessageText');
 
  Result := True;
end;




function GetDnsResponderUrl(): String;
var
  xpathTools: Variant; 
  dnsResponderUrl: String;
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    xpathTools.XmlFilePath := ExpandConstant('{app}') + '\DirectDnsResponderSvc.exe.config' ;
    dnsResponderUrl := xpathTools.SelectSingleAttribute('/configuration/ServiceSettingsGroup/RecordRetrievalServiceSettings/@Url');
    Result := dnsResponderUrl;
end;



function GetConfigAdminEndPoint(bindConfigName : String): String;
var
  xpathTools: Variant; 
  endPoint: String;
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    xpathTools.XmlFilePath := ExpandConstant('{app}') + '\ConfigUI\Config\client.config' ;
    endPoint := xpathTools.SelectSingleAttribute('/client/endpoint[@bindingConfiguration="' + bindConfigName + '"]/@address');
    Result := endPoint;
end;



function GetGatewaySetting(xpath : String): String;
var
  xpathTools: Variant;    
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    xpathTools.XmlFilePath := ExpandConstant('{app}') + '\SmtpAgentConfig.xml' ;
    Result := xpathTools.SelectSingleAttribute(xpath);
end;


       
procedure GatewayOnClick (Sender: TObject);
var                   
  EndPointsButton: TNewButton;
  buttonCaption : String;
  GatewayAdminPage : TWizardPage;
begin         
  EndPointsButton := TNewButton(Sender);
  GatewayAdminPage := TWizardPage(EndPointsButton.Owner);
  buttonCaption := EndPointsButton.Caption;
  EndPointsButton.Caption := 'Checking...';
  try      
    RunTestConnect(GatewayAdminPage, 'DomainManagerLabel', 'DomainManagerText');
    RunTestConnect(GatewayAdminPage, 'AddressManagerLabel', 'AddressManagerText');
    RunTestConnect(GatewayAdminPage, 'PrivateCertsLabel', 'PrivateCertsText');
    RunTestConnect(GatewayAdminPage, 'AnchorsLabel', 'AnchorsText');
    //RunTestConnect(GatewayAdminPage, 'DnsResolverIpLabel', 'DnsResolverIpText');    
  except         
    RaiseException(GetExceptionMessage);
  finally
    EndPointsButton.Caption := buttonCaption;
    EndPointsButton.Update;  
  end;   
end;




procedure ConfigAdminHostNameOnClick(Sender: TObject);
var
  buttonCaption : String;  
  HostNameTextBox : TNewEdit;
  ConfigAdminPage : TWizardPage;
  tools : Variant;
  EndPointsButton : TNewButton;
begin
  try                              
    tools := CreateOleObject('Direct.Installer.UrlTools');
  except
    RaiseException('Cannot find Direct.Installer.UrlTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;  
  
  EndPointsButton := TNewButton(Sender);
  ConfigAdminPage := TWizardPage(EndPointsButton.Owner);
  HostNameTextBox := TNewEdit(ConfigAdminPage.FindComponent('HostNameTextBox'));
  
  buttonCaption := EndPointsButton.Caption;
  EndPointsButton.Caption := 'Checking...';
   
  try      
    RunTestConnect(ConfigAdminPage, 'CertificatesLabel', 'CertificatesUrlText');
    RunTestConnect(ConfigAdminPage, 'AnchorsLabel', 'AnchorsUrlText');
    RunTestConnect(ConfigAdminPage, 'AddressesLabel', 'AddressesUrlText');
    RunTestConnect(ConfigAdminPage, 'DomainsLabel', 'DomainsUrlText');
    RunTestConnect(ConfigAdminPage, 'DnsRecordsLabel', 'DnsRecordsUrlText');
    RunTestConnect(ConfigAdminPage, 'AuthenticationLabel', 'AuthenticationUrlText'); 
  except         
    RaiseException(GetExceptionMessage);
  finally
    EndPointsButton.Caption := buttonCaption;
    EndPointsButton.Update;  
  end;
end;








procedure DnsResponderPageOnActivate(Sender: TWizardPage);
var
  tools : Variant;
  DnsServiceUrlText : TNewEdit;
  DnsServiceTestTextBox : TNewEdit;
begin
  //Set DnsService from config file.
  DnsServiceUrlText := TNewEdit(Sender.FindComponent('DnsServiceUrlText'));
  DnsServiceUrlText.Text :=   GetDnsResponderUrl();
  
  //Set TestService.aspx by rebuilding Url 
  try                              
    tools := CreateOleObject('Direct.Installer.UrlTools');
  except
    RaiseException('Cannot find Direct.Installer.UrlTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;  
  DnsServiceTestTextBox := TNewEdit(Sender.FindComponent('DnsServiceTestTextBox')); 
  DnsServiceTestTextBox.Text := tools.UpdateUrlPathAndQuery(DnsServiceUrlText.Text, '/DnsService/TestService.aspx').FullUrl;
  
end;



function GetDbConnStr(): String;
var
  xpathTools: Variant; 
  dnsResponderUrl: String;
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    xpathTools.XmlFilePath := ExpandConstant('{app}') + '\ConfigService\Web.Config' ;
    dnsResponderUrl := xpathTools.SelectSingleAttribute('configuration/connectionStrings/add[@name="configStore"]/@connectionString');
    Result := dnsResponderUrl;
end;



procedure DatabaseConnPageOnActivate(Sender: TWizardPage);
var
  DbConnStrTextBox : TNewEdit;
begin
  DbConnStrTextBox := TNewEdit(Sender.FindComponent('DbConnStrTextBox')); 
  DbConnStrTextBox.Text := GetDbConnStr();
end;





procedure UpdateConfigFileName();
var
  xpathTools: Variant;
begin
  try                              
    xpathTools := CreateOleObject('Direct.Installer.XPathTools');
  except
    RaiseException('Cannot find Direct.Installer.XPathTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    xpathTools.XmlFilePath := ExpandConstant('{app}') + '\ConfigUI\Web.Config' ;
    xpathTools.SetSingleAttribute('configuration/system.serviceModel/client/@configSource', 'Config\Client.config');
end;


procedure ConfigAdminPageOnActivate(Sender: TWizardPage);
var               
  CertificatesUrlText, AnchorsUrlText, AddressesUrlText, DomainsUrlText, DnsRecordsUrlText, AuthenticationUrlText : TCustomEdit;
begin
   
  CertificatesUrlText := TCustomEdit(Sender.FindComponent('CertificatesUrlText')); 
  CertificatesUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_ICertificateStore');
  

  AnchorsUrlText := TCustomEdit(Sender.FindComponent('AnchorsUrlText')); 
  AnchorsUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_IAnchorStore');
  

  AddressesUrlText := TCustomEdit(Sender.FindComponent('AddressesUrlText')); 
  AddressesUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_IAddressManager');
 

  DomainsUrlText := TCustomEdit(Sender.FindComponent('DomainsUrlText'));
  DomainsUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_IDomainManager');
  

  DnsRecordsUrlText := TCustomEdit(Sender.FindComponent('DnsRecordsUrlText')); 
  DnsRecordsUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_IDnsRecordManager');
  

  AuthenticationUrlText := TCustomEdit(Sender.FindComponent('AuthenticationUrlText')); 
  AuthenticationUrlText.Text := GetConfigAdminEndPoint('BasicHttpBinding_IAuthManager'); 
 

end;




procedure GatewayAdminPageOnActivate(Sender: TWizardPage);
var
  DomainManagerText, AddressManagerText, PrivateCertsText, AnchorsText, DnsResolverIpText: TNewMemo;
  DomainText: TNewEdit;
begin

 
  DomainText := TNewEdit(Sender.FindComponent('DomainText'));
  DomainText.Text := GetGatewaySetting('/SmtpAgentConfig/Domain');

  DomainManagerText := TNewMemo(Sender.FindComponent('DomainManagerText'));
  DomainManagerText.Text := GetGatewaySetting('/SmtpAgentConfig/DomainManager/Url');

  AddressManagerText := TNewMemo(Sender.FindComponent('AddressManagerText'));
  AddressManagerText.Text := GetGatewaySetting('/SmtpAgentConfig/AddressManager/Url');
  
  PrivateCertsText := TNewMemo(Sender.FindComponent('PrivateCertsText'));
  PrivateCertsText.Text := GetGatewaySetting('/SmtpAgentConfig/PrivateCerts/ServiceResolver/ClientSettings/Url');
  
  AnchorsText := TNewMemo(Sender.FindComponent('AnchorsText'));
  AnchorsText.Text := GetGatewaySetting('/SmtpAgentConfig/Anchors/ServiceResolver/ClientSettings/Url');
  
  DnsResolverIpText := TNewMemo(Sender.FindComponent('DnsResolverIpText'));
  DnsResolverIpText.Text := GetGatewaySetting('/SmtpAgentConfig/PublicCerts/DnsResolver/ServerIP');

end; 



procedure ConfigSmtpAdminPageOnActivate(Sender: TWizardPage);
var
  PickupText, RawMessageText, BadMessageText, IncommingMessageText, OutgoingMessageText : TNewMemo;
begin
   
  PickupText := TNewMemo(Sender.FindComponent('PickupText'));
  PickupText.Text := GetGatewaySetting('/SmtpAgentConfig/InternalMessage/PickupFolder');

  RawMessageText := TNewMemo(Sender.FindComponent('RawMessageText'));
  RawMessageText.Text := GetGatewaySetting('/SmtpAgentConfig/RawMessage/CopyFolder');
  
  BadMessageText := TNewMemo(Sender.FindComponent('BadMessageText'));
  BadMessageText.Text := GetGatewaySetting('/SmtpAgentConfig/BadMessage/CopyFolder');
  
  IncommingMessageText := TNewMemo(Sender.FindComponent('IncommingMessageText'));
  IncommingMessageText.Text := GetGatewaySetting('/SmtpAgentConfig/ProcessIncoming/CopyFolder');
  
  OutgoingMessageText := TNewMemo(Sender.FindComponent('OutgoingMessageText'));
  OutgoingMessageText.Text := GetGatewaySetting('/SmtpAgentConfig/ProcessOutgoing/CopyFolder');

end;





function DnsResponderPage_ShouldSkip(Page: TwizardPage): Boolean;
begin
  Result := (pos( 'dnsresponder', WizardSelectedComponents( false)) = 0) ;  
end;

//Skip this page if not configuring a service that relies on the database.
function DatabaseConnPage_ShouldSkip(Page: TwizardPage): Boolean;
begin
  Result := ((pos( 'dnswebservice', WizardSelectedComponents( false)) = 0)
              and (pos( 'configwebservice', WizardSelectedComponents( false)) = 0))
                or (pos( 'developergateway', WizardSelectedComponents( false)) > 0)     
end;

//Skip this page is not configuring the Config Admin UI
function ConfigAdminPage_ShouldSkip(Page: TwizardPage): Boolean;
begin
  Result := (pos( 'configui', WizardSelectedComponents( false)) = 0)                  
                or (pos( 'developergateway', WizardSelectedComponents( false)) > 0)
end;


//Skip this page is not configuring the Config Admin UI
function GatewayAdminPage_ShouldSkip(Page: TwizardPage): Boolean;
begin
  Result := (pos( 'directgateway', WizardSelectedComponents( false)) = 0)                  
                or (pos( 'developergateway', WizardSelectedComponents( false)) > 0)
end;



//only call this once.  Notice we register the Health.Direct.Install.Tools.dll via the temp folder once then remove.
//later it is registered when the files have been placed in their deployment location.
function MsSmtpServiceExists(Host: String; Port: Integer): Boolean;
var
    ResultCode: Integer;
    SmtpExists: Boolean;
    SmtpTools: Variant;       
begin
  
  if(CommandlineParamExists('SkipSmtpCheck')) then
  begin
    Result := true;
    exit;
  end
  if(not toolsRegistered) then
  begin
    ExtractTemporaryFile('Health.Direct.Install.Tools.dll');     
    Exec(ExpandConstant('{dotnet2032}\RegAsm.exe'),'Health.Direct.Install.Tools.dll /codebase', ExpandConstant('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode );
    Exec(ExpandConstant('{dotnet2064}\RegAsm.exe'),'Health.Direct.Install.Tools.dll /codebase', ExpandConstant('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode );
    toolsRegistered := true;
  end

  try                           
    SmtpTools := CreateOleObject('Direct.Installer.SmtpTools');
  except
    RaiseException('Cannot find Direct.Installer.SmtpTools.'#13#13'(Error ''' + GetExceptionMessage + ''' occurred)');
  end;
    try
      Log('Checking Smtp connection with host:port of ' + Host + ':' + IntToStr(Port)); 
      SmtpExists := SmtpTools.TestConnection(Host, Port);
    except
      Log('Error: ' + GetExceptionMessage);       
    end
    Result := SmtpExists;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
  
 begin
  if(CurPageID = wpSelectComponents) then 
    begin                
      //check for smtp services
      if (pos( 'directgateway', WizardSelectedComponents( false)) > 0)  or (pos( 'development', WizardSetupType( false)) > 0) then  
      begin
          if not (MsSmtpServiceExists('LocalHost', 25)) then //Because we are always installing locally this should suffice for now.
          begin
            MsgBox('Failed to find smtp running.' #13#13 + 'Cannot install directgateway.', mbInformation, mb_Ok);
            Result := False;
          end 
          else begin
            Result := True;
          end;
      end
      else begin
        Result := True;
      end;      
  end    
  else begin
    Result := True;
  end;    
end;


procedure DnsHelpButtonOnClick(Sender: TObject);     
begin
  MsgBox('Test connectivity to the Dns Web Service and its database connectivity.'  #13#10 +
    'Set the Dns Web Service endpoint used by the DnsResponder Windows Service.'  #13#10 +
    'The "End Point" labels will be green when they are connecting and red when failing.', mbInformation, mb_Ok);
end;

procedure DatabaseHelpButtonOnClick(Sender: TObject);   
begin
  MsgBox('Set database connection strings on the ConfigService and/or DnsService.'  #13#10 +
    'Use the Test button to test connectivity before clicking next.  Clicking next will persist the connection string.' #13#10 +
    'When connecting across machine boundaries with only TCP/IP protocol enabled it may help to add Network Library=dbmssocn to your connection string.' , mbInformation, mb_Ok);
end;


procedure ConfigAdminHelpButtonOnClick(Sender: TObject);       
begin
  MsgBox('Set the web service client endpoints on the Admin Configuration web application.  '  #13#10 +
    'The "End Point" labels will be green when they are connecting and red when failing.', mbInformation, mb_Ok);
end;

procedure GatewayAdminHelpButtonOnClick(Sender: TObject);
begin
  MsgBox('Part I configures Domains and endpoints.'  #13#10 +
    'Part II configures message folders and SMTP pickup up folder.' #13#10 +
    'Note: Both sending messges to SMTP server and dropping in the pickup folder are supported for sending messages.' #13#10 +
    'The "End Point" labels will be green when they are connecting and red when failing.', mbInformation, mb_Ok);
end;


procedure URLLabelOnClick(Sender: TObject);
var
  ErrorCode: Integer;
  URLLabel: TNewStaticText;
  begin
  URLLabel := TNewStaticText(Sender);
  ShellExecAsOriginalUser('open', URLLabel.Caption, '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
end;


function CreateDatabaseConnWizardPage(): TWizardPage;
var
  DatabaseConnPage: TWizardPage;       
  Button, HelpButton: TNewButton;
  DbConnStrTextBox : TNewEdit;
  StatusLabel: TNewStaticText;
begin

  DatabaseConnPage := CreateCustomPage(wpInfoAfter, 'Configure database connection string', '');
    
  HelpButton := TNewButton.Create(DatabaseConnPage);      
  HelpButton.Left := DatabaseConnPage.Surface.Width - ScaleX(20);
  HelpButton.Width := ScaleX(20);
  HelpButton.Height := ScaleY(20);   
  HelpButton.Caption := '?';
  HelpButton.OnClick := @DatabaseHelpButtonOnClick;
  HelpButton.Parent := DatabaseConnPage.Surface;  

  Button := TNewButton.Create(DatabaseConnPage);
  Button.Top := HelpButton.Top + HelpButton.Height + ScaleY(20);
  Button.Height :=  WizardForm.NextButton.Height;
  Button.Width := DatabaseConnPage.SurfaceWidth div 4;
  Button.Caption := 'Test: ';
  Button.OnClick := @CheckDatabaseConnOnClick;
  Button.Parent := DatabaseConnPage.Surface;

  DbConnStrTextBox := TNewEdit.Create(DatabaseConnPage);
  DbConnStrTextBox.Name := 'DbConnStrTextBox';
  DbConnStrTextBox.Top := Button.Top + Button.Height + ScaleY(20);
  DbConnStrTextBox.Width := DatabaseConnPage.SurfaceWidth - ScaleX(8);
  DbConnStrTextBox.Parent := DatabaseConnPage.Surface;
  
  StatusLabel := TNewStaticText.Create(DatabaseConnPage);
  StatusLabel.Name := 'StatusLabel';
  StatusLabel.Caption := '';
  StatusLabel.Top := DatabaseConnPage.SurfaceHeight - ScaleY(16);
  StatusLabel.Left := ScaleX(8);
  StatusLabel.Parent := DatabaseConnPage.Surface;

  DatabaseConnPage.OnActivate := @DatabaseConnPageOnActivate;
  DatabaseConnPage.OnNextButtonClick := @SetDatabaseConnUrlOnClick;
  DatabaseConnPage.OnShouldSkipPage := @DatabaseConnPage_ShouldSkip;

  Result := DatabaseConnPage;
end;





function CreateDnsResponderWizardPage(pageBefore: TWizardPage): TWizardPage;
var
  DnsResponderPage: TWizardPage;
  DnsServiceTestTextBox: TNewEdit;
  Button, HelpButton: TNewButton;
  DnsServiceEndPointLabel, DnsServiceLabel, DnsServiceTestLabel : TNewStaticText;
  DnsServiceUrlText : TNewEdit;
begin

  DnsResponderPage := CreateCustomPage(pageBefore.ID, 'Configure DnsResponder', 'DnsService endpoint stored in DirectDnsResponderSvc.exe.config');
    
  HelpButton := TNewButton.Create(DnsResponderPage);      
  HelpButton.Left := DnsResponderPage.Surface.Width - ScaleX(20);
  HelpButton.Width := ScaleX(20);
  HelpButton.Height := ScaleY(20);   
  HelpButton.Caption := '?';
  HelpButton.OnClick := @DnsHelpButtonOnClick;
  HelpButton.Parent := DnsResponderPage.Surface;  

  Button := TNewButton.Create(DnsResponderPage);
  Button.Top := HelpButton.Top + HelpButton.Height + ScaleY(20);
  Button.Height :=  WizardForm.NextButton.Height;
  Button.Width := DnsResponderPage.SurfaceWidth div 4;
  Button.Caption := 'Test: ';
  Button.OnClick := @CheckDnsResponderServiceOnClick;
  Button.Parent := DnsResponderPage.Surface;
  
  DnsServiceEndPointLabel := TNewStaticText.Create(DnsResponderPage);
  DnsServiceEndPointLabel.Top := Button.Top + Button.Height + ScaleY(16); 
  DnsServiceEndPointLabel.Caption := 'DnsService endpoint: ';
  DnsServiceEndPointLabel.Width := Button.Width;
  DnsServiceEndPointLabel.Parent := DnsResponderPage.Surface;

  DnsServiceTestLabel := TNewStaticText.Create(DnsResponderPage);
  DnsServiceTestLabel.Name := 'DnsServiceTestLabel';
  DnsServiceTestLabel.Caption := 'Test DB connectivity: ';
  DnsServiceTestLabel.Top := DnsServiceEndPointLabel.Top + DnsServiceEndPointLabel.Height + ScaleY(16);
  DnsServiceTestLabel.Parent := DnsResponderPage.Surface;

  DnsServiceTestTextBox := TNewEdit.Create(DnsResponderPage);
  DnsServiceTestTextBox.Name := 'DnsServiceTestTextBox';
  DnsServiceTestTextBox.Top := DnsServiceTestLabel.Top;
  DnsServiceTestTextBox.Left :=  DnsServiceTestLabel.Width + ScaleX(8);
  DnsServiceTestTextBox.Width := DnsResponderPage.SurfaceWidth - (DnsServiceTestLabel.Left + DnsServiceTestLabel.Width);
  DnsServiceTestTextBox.Parent := DnsResponderPage.Surface;  
                                             
  DnsServiceLabel := TNewStaticText.Create(DnsResponderPage);
  DnsServiceLabel.Name := 'DnsServiceLabel';
  DnsServiceLabel.Caption := 'DNS Service';
  DnsServiceLabel.Top := DnsServiceTestLabel.Top + DnsServiceTestLabel.Height + ScaleY(16); 
  DnsServiceLabel.Parent := DnsResponderPage.Surface;

  DnsServiceUrlText := TNewEdit.Create(DnsResponderPage);
  DnsServiceUrlText.Name := 'DnsServiceUrlText';   
  DnsServiceUrlText.Top := DnsServiceLabel.Top;
  DnsServiceUrlText.Left :=  DnsServiceLabel.Left + DnsServiceLabel.Width + ScaleX(8);
  DnsServiceUrlText.Width := DnsResponderPage.SurfaceWidth - DnsServiceLabel.Width - ScaleX(8);  
  DnsServiceUrlText.Parent := DnsResponderPage.Surface;     
  
  DnsResponderPage.OnActivate := @DnsResponderPageOnActivate;
  DnsResponderPage.OnNextButtonClick := @SetDnsResponderUrlOnClick;
  DnsResponderPage.OnShouldSkipPage := @DnsResponderPage_ShouldSkip;
  Result := DnsResponderPage;
end;






function CreateGatewayWizardPage(pageBefore: TWizardPage): TWizardPage;
var
  GatewayAdminPage: TWizardPage;
  HelpButton, EndPointsButton: TNewButton;
  DomainLabel, DomainManagerLabel, AddressManagerLabel, PrivateCertsLabel, AnchorsLabel, DnsResolverIpLabel: TNewStaticText;
  DomainText : TNewEdit;
  DomainManagerText, AddressManagerText, PrivateCertsText, AnchorsText, DnsResolverIpText: TNewMemo;
  
begin
    GatewayAdminPage := CreateCustomPage(pageBefore.ID, 'Configure Gateway part I (SMTP)', '');
     
    HelpButton := TNewButton.Create(GatewayAdminPage);      
    HelpButton.Left := GatewayAdminPage.Surface.Width - ScaleX(20);
    HelpButton.Width := ScaleX(20);
    HelpButton.Height := ScaleY(20);   
    HelpButton.Caption := '?';
    HelpButton.OnClick := @GatewayAdminHelpButtonOnClick;
    HelpButton.Parent := GatewayAdminPage.Surface;  
    
    //Set Domains
    DomainLabel := TNewStaticText.Create(GatewayAdminPage);
    DomainLabel.Name := 'DomainLabel';
    DomainLabel.Top :=  HelpButton.Top + HelpButton.Height + ScaleY(14);
    DomainLabel.Caption := 'Domain Name: ';
    DomainLabel.Parent := GatewayAdminPage.Surface;
                              
    DomainText := TNewEdit.Create(GatewayAdminPage);
    DomainText.Name := 'DomainText';
    DomainText.Top := HelpButton.Top + HelpButton.Height + ScaleY(14);
    DomainText.Left := DomainLabel.Width + ScaleX(8);
    DomainText.Width := GatewayAdminPage.SurfaceWidth - ScaleX(8);
    DomainText.Parent := GatewayAdminPage.Surface;

    //Test button
    EndPointsButton := TNewButton.Create(GatewayAdminPage);
    EndPointsButton.Top :=  DomainLabel.Top + DomainLabel.Height + ScaleY(14);
    EndPointsButton.Width :=  GatewayAdminPage.Surface.Width div 4;
    EndPointsButton.Caption := 'Test End Points:';
    EndPointsButton.OnClick := @GatewayOnClick;
    EndPointsButton.Parent := GatewayAdminPage.Surface;

    //Set Privates Certs.  Later it is placed in its page location.  Using its width to base all the texbox left positions.       
    PrivateCertsLabel := TNewStaticText.Create(GatewayAdminPage);
    PrivateCertsLabel.Caption := 'Certificate Resolver: ';

    //Set Domain Manager
    DomainManagerLabel := TNewStaticText.Create(GatewayAdminPage);
    DomainManagerLabel.Name := 'DomainManagerLabel';
    DomainManagerLabel.Top :=  EndPointsButton.Top + EndPointsButton.Height + ScaleY(14);
    DomainManagerLabel.Caption := 'Domain Manager: ';
    DomainManagerLabel.Parent := GatewayAdminPage.Surface;
                              
    DomainManagerText := TNewMemo.Create(GatewayAdminPage);
    DomainManagerText.Name := 'DomainManagerText';
    DomainManagerText.Top := EndPointsButton.Top + EndPointsButton.Height + ScaleY(14);
    DomainManagerText.Left := PrivateCertsLabel.Width + ScaleX(8);
    DomainManagerText.Width := GatewayAdminPage.SurfaceWidth - DomainManagerLabel.Width - ScaleX(8);
    DomainManagerText.Height := DomainManagerText.Height div 4;
    DomainManagerText.WordWrap := false;
    DomainManagerText.WantReturns := false;
    DomainManagerText.Parent := GatewayAdminPage.Surface;


    //Set Address Manager
    AddressManagerLabel := TNewStaticText.Create(GatewayAdminPage);
    AddressManagerLabel.Name := 'AddressManagerLabel';
    AddressManagerLabel.Top :=  DomainManagerLabel.Top + DomainManagerLabel.Height + ScaleY(14);
    AddressManagerLabel.Caption := 'Address Manager: ';
    AddressManagerLabel.Parent := GatewayAdminPage.Surface;
                              
    AddressManagerText := TNewMemo.Create(GatewayAdminPage);
    AddressManagerText.Name := 'AddressManagerText';
    AddressManagerText.Top := DomainManagerLabel.Top + DomainManagerLabel.Height + ScaleY(14);
    AddressManagerText.Left := DomainManagerText.Left;
    AddressManagerText.Width := DomainManagerText.Width;
    AddressManagerText.Height := AddressManagerText.Height div 4;
    AddressManagerText.WordWrap := false;
    AddressManagerText.WantReturns := false;
    AddressManagerText.Parent := GatewayAdminPage.Surface;

    //Set Privates Certs (Created above)                
    PrivateCertsLabel.Name := 'PrivateCertsLabel';
    PrivateCertsLabel.Top :=  AddressManagerLabel.Top + AddressManagerLabel.Height + ScaleY(14);
    PrivateCertsLabel.Parent := GatewayAdminPage.Surface;
                              
    PrivateCertsText := TNewMemo.Create(GatewayAdminPage);
    PrivateCertsText.Name := 'PrivateCertsText';
    PrivateCertsText.Top := AddressManagerLabel.Top + AddressManagerLabel.Height + ScaleY(14);
    PrivateCertsText.Left := DomainManagerText.Left;
    PrivateCertsText.Width := DomainManagerText.Width;
    PrivateCertsText.Height := PrivateCertsText.Height div 4;
    PrivateCertsText.WordWrap := false;
    PrivateCertsText.WantReturns := false;
    PrivateCertsText.Parent := GatewayAdminPage.Surface;

    //Set Anchors
    AnchorsLabel := TNewStaticText.Create(GatewayAdminPage);
    AnchorsLabel.Name := 'AnchorsLabel';
    AnchorsLabel.Top :=  PrivateCertsLabel.Top + PrivateCertsLabel.Height + ScaleY(14);
    AnchorsLabel.Caption := 'Anchor Resolver: ';
    AnchorsLabel.Parent := GatewayAdminPage.Surface;
                              
    AnchorsText := TNewMemo.Create(GatewayAdminPage);
    AnchorsText.Name := 'AnchorsText';
    AnchorsText.Top := PrivateCertsLabel.Top + PrivateCertsLabel.Height + ScaleY(14);
    AnchorsText.Left := DomainManagerText.Left;
    AnchorsText.Width := DomainManagerText.Width;
    AnchorsText.Height := AnchorsText.Height div 4;
    AnchorsText.WordWrap := false;
    AnchorsText.WantReturns := false;
    AnchorsText.Parent := GatewayAdminPage.Surface;

    //Set DnsResolverIp
    DnsResolverIpLabel := TNewStaticText.Create(GatewayAdminPage);
    DnsResolverIpLabel.Name := 'DnsResolverIpLabel';
    DnsResolverIpLabel.Top :=  AnchorsLabel.Top + AnchorsLabel.Height + ScaleY(14);
    DnsResolverIpLabel.Caption := 'Dns Resolver IP: ';
    DnsResolverIpLabel.Parent := GatewayAdminPage.Surface;
                              
    DnsResolverIpText := TNewMemo.Create(GatewayAdminPage);
    DnsResolverIpText.Name := 'DnsResolverIpText';
    DnsResolverIpText.Top := AnchorsLabel.Top + AnchorsLabel.Height + ScaleY(14);
    DnsResolverIpText.Left := DomainManagerText.Left;
    DnsResolverIpText.Width := DomainManagerText.Width;
    DnsResolverIpText.Height := DnsResolverIpText.Height div 4;
    DnsResolverIpText.WordWrap := false;
    DnsResolverIpText.WantReturns := false;
    DnsResolverIpText.Parent := GatewayAdminPage.Surface;


    GatewayAdminPage.OnActivate := @GatewayAdminPageOnActivate;
    GatewayAdminPage.OnNextButtonClick := @SetGatewayConfigOnClick;
    GatewayAdminPage.OnShouldSkipPage := @GatewayAdminPage_ShouldSkip;

    Result := GatewayAdminPage;
end;




function CreateGatewaySmtpWizardPage(pageBefore: TWizardPage): TWizardPage;
var
  GatewaySmtpAdminPage: TWizardPage;
  HelpButton: TNewButton;
  PickupLabel, RawMessageLabel, BadMessageLabel, IncommingMessageLabel, OutgoingMessageLabel : TNewStaticText;
  PickupText, RawMessageText, BadMessageText, IncommingMessageText, OutgoingMessageText : TNewMemo;
begin
    GatewaySmtpAdminPage := CreateCustomPage(pageBefore.ID, 'Configure Gateway part II', '');

    HelpButton := TNewButton.Create(GatewaySmtpAdminPage);      
    HelpButton.Left := GatewaySmtpAdminPage.Surface.Width - ScaleX(20);
    HelpButton.Width := ScaleX(20);
    HelpButton.Height := ScaleY(20);   
    HelpButton.Caption := '?';
    HelpButton.OnClick := @GatewayAdminHelpButtonOnClick;
    HelpButton.Parent := GatewaySmtpAdminPage.Surface;  
         
    //Set Pickup Folder
    PickupLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    PickupLabel.Name := 'DomainLabel';
    PickupLabel.Top :=  HelpButton.Top + HelpButton.Height + ScaleY(14);
    PickupLabel.Caption := 'SMTP Message Pickup Folder: ';
    PickupLabel.Parent := GatewaySmtpAdminPage.Surface;
                              
    PickupText := TNewMemo.Create(GatewaySmtpAdminPage);
    PickupText.Name := 'PickupText';
    PickupText.Top := HelpButton.Top + HelpButton.Height + ScaleY(14);
    PickupText.Left := PickupLabel.Width + ScaleX(8);
    PickupText.Width := GatewaySmtpAdminPage.SurfaceWidth - ScaleX(8);
    PickupText.Height := PickupText.Height div 4;
    PickupText.WordWrap := false;
    PickupText.WantReturns := false;
    PickupText.Parent := GatewaySmtpAdminPage.Surface;

    //Set Raw Message folder
    RawMessageLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    RawMessageLabel.Name := 'RawMessageLabel';
    RawMessageLabel.Top :=  PickupLabel.Top + PickupLabel.Height + ScaleY(14);
    RawMessageLabel.Caption := 'Raw Message Folder: ';
    RawMessageLabel.Parent := GatewaySmtpAdminPage.Surface;
                              
    RawMessageText := TNewMemo.Create(GatewaySmtpAdminPage);
    RawMessageText.Name := 'RawMessageText';
    RawMessageText.Top := RawMessageLabel.Top;
    RawMessageText.Left := PickupLabel.Width + ScaleX(8);
    RawMessageText.Width := GatewaySmtpAdminPage.SurfaceWidth - ScaleX(8);
    RawMessageText.Height := RawMessageText.Height div 4;
    RawMessageText.WordWrap := false;
    RawMessageText.WantReturns := false;
    RawMessageText.Parent := GatewaySmtpAdminPage.Surface;
    
    //Set Bad Message Folder
    BadMessageLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    BadMessageLabel.Name := 'BadMessageLabel';
    BadMessageLabel.Top :=  RawMessageLabel.Top + RawMessageLabel.Height + ScaleY(14);
    BadMessageLabel.Caption := 'Bad Message Folder: ';
    BadMessageLabel.Parent := GatewaySmtpAdminPage.Surface;
                              
    BadMessageText := TNewMemo.Create(GatewaySmtpAdminPage);
    BadMessageText.Name := 'BadMessageText';
    BadMessageText.Top := BadMessageLabel.Top;
    BadMessageText.Left := PickupLabel.Width + ScaleX(8);
    BadMessageText.Width := GatewaySmtpAdminPage.SurfaceWidth - ScaleX(8);
    BadMessageText.Height := BadMessageText.Height div 4;
    BadMessageText.WordWrap := false;
    BadMessageText.WantReturns := false;
    BadMessageText.Parent := GatewaySmtpAdminPage.Surface;

    //Set Incomming Message Folder
    IncommingMessageLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    IncommingMessageLabel.Name := 'IncommingMessageLabel';
    IncommingMessageLabel.Top :=  BadMessageLabel.Top + BadMessageLabel.Height + ScaleY(14);
    IncommingMessageLabel.Caption := 'Incomming Message Folder: ';
    IncommingMessageLabel.Parent := GatewaySmtpAdminPage.Surface;
                              
    IncommingMessageText := TNewMemo.Create(GatewaySmtpAdminPage);
    IncommingMessageText.Name := 'IncommingMessageText';
    IncommingMessageText.Top := IncommingMessageLabel.Top;
    IncommingMessageText.Left := PickupLabel.Width + ScaleX(8);
    IncommingMessageText.Width := GatewaySmtpAdminPage.SurfaceWidth - ScaleX(8);
    IncommingMessageText.Height := IncommingMessageText.Height div 4;
    IncommingMessageText.WordWrap := false;
    IncommingMessageText.WantReturns := false;
    IncommingMessageText.Parent := GatewaySmtpAdminPage.Surface;

    //Set Outgoing Message Folder
    OutgoingMessageLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    OutgoingMessageLabel.Name := 'OutgoingMessageLabel';
    OutgoingMessageLabel.Top :=  IncommingMessageLabel.Top + IncommingMessageLabel.Height + ScaleY(14);
    OutgoingMessageLabel.Caption := 'Outgoing Message Folder: ';
    OutgoingMessageLabel.Parent := GatewaySmtpAdminPage.Surface;
                              
    OutgoingMessageText := TNewMemo.Create(GatewaySmtpAdminPage);
    OutgoingMessageText.Name := 'OutgoingMessageText';
    OutgoingMessageText.Top := OutgoingMessageLabel.Top;
    OutgoingMessageText.Left := PickupLabel.Width + ScaleX(8);
    OutgoingMessageText.Width := GatewaySmtpAdminPage.SurfaceWidth - ScaleX(8);
    OutgoingMessageText.Height := OutgoingMessageText.Height div 4;
    OutgoingMessageText.WordWrap := false;
    OutgoingMessageText.WantReturns := false;
    OutgoingMessageText.Parent := GatewaySmtpAdminPage.Surface;

    OutgoingMessageLabel := TNewStaticText.Create(GatewaySmtpAdminPage);
    OutgoingMessageLabel.Caption := 'Outgoing Folder: ';
        
    GatewaySmtpAdminPage.OnActivate := @ConfigSmtpAdminPageOnActivate;
    GatewaySmtpAdminPage.OnNextButtonClick := @SetConfigSmtpAdminEndpointsOnClick;
    GatewaySmtpAdminPage.OnShouldSkipPage := @GatewayAdminPage_ShouldSkip;

    Result := GatewaySmtpAdminPage;
end;


function CreateUIConfigWizardPage(pageBefore: TWizardPage): TWizardPage;
var
  ConfigAdminPage: TWizardPage;       
  HelpButton, EndPointsButton: TNewButton;  
  CertificatesLabel, AnchorsLabel, AddressesLabel, DomainsLabel, DnsRecordsLabel, AuthenticationLabel : TNewStaticText;
  CertificatesUrlText, AnchorsUrlText, AddressesUrlText, DomainsUrlText, DnsRecordsUrlText, AuthenticationUrlText : TNewEdit;
  
begin

  ConfigAdminPage := CreateCustomPage(pageBefore.ID, 'Configure Config Admin', '');
    
  HelpButton := TNewButton.Create(ConfigAdminPage);      
  HelpButton.Left := ConfigAdminPage.Surface.Width - ScaleX(20);
  HelpButton.Width := ScaleX(20);
  HelpButton.Height := ScaleY(20);   
  HelpButton.Caption := '?';
  HelpButton.OnClick := @ConfigAdminHelpButtonOnClick;
  HelpButton.Parent := ConfigAdminPage.Surface;  
    
  EndPointsButton := TNewButton.Create(ConfigAdminPage);
  EndPointsButton.Top :=  HelpButton.Top + HelpButton.Height + ScaleY(14);
  EndPointsButton.Width :=  ConfigAdminPage.Surface.Width div 4;
  EndPointsButton.Caption := 'Test End Points:';
  EndPointsButton.OnClick := @ConfigAdminHostNameOnClick;
  EndPointsButton.Parent := ConfigAdminPage.Surface;

  CertificatesLabel := TNewStaticText.Create(ConfigAdminPage);
  CertificatesLabel.Name := 'CertificatesLabel'
  CertificatesLabel.Caption := 'Certificates: ';
  CertificatesLabel.Top := EndPointsButton.Top + EndPointsButton.Height + ScaleY(14);
  CertificatesLabel.Parent := ConfigAdminPage.Surface;

  CertificatesUrlText := TNewEdit.Create(ConfigAdminPage);
  CertificatesUrlText.Name := 'CertificatesUrlText';
  CertificatesUrlText.Top := CertificatesLabel.Top;
  CertificatesUrlText.Left := CertificatesLabel.Left + CertificatesLabel.Width + ScaleX(8);
  CertificatesUrlText.Width := ConfigAdminPage.SurfaceWidth - CertificatesLabel.Width - ScaleX(8);
  CertificatesUrlText.Parent := ConfigAdminPage.Surface;
   

  AnchorsLabel := TNewStaticText.Create(ConfigAdminPage);
  AnchorsLabel.Name := 'AnchorsLabel';
  AnchorsLabel.Caption := 'Anchors: ';
  AnchorsLabel.Top := CertificatesLabel.Top + CertificatesLabel.Height + ScaleY(14);
  AnchorsLabel.Parent := ConfigAdminPage.Surface;

  AnchorsUrlText := TNewEdit.Create(ConfigAdminPage);
  AnchorsUrlText.Name := 'AnchorsUrlText';
  AnchorsUrlText.Top := AnchorsLabel.Top;
  AnchorsUrlText.Left := CertificatesUrlText.Left;
  AnchorsUrlText.Width := CertificatesUrlText.Width;
  AnchorsUrlText.Parent := ConfigAdminPage.Surface;


  AddressesLabel := TNewStaticText.Create(ConfigAdminPage);
  AddressesLabel.Name := 'AddressesLabel';
  AddressesLabel.Caption := 'Addresses: ';
  AddressesLabel.Top := AnchorsLabel.Top + AnchorsLabel.Height + ScaleY(14);
  AddressesLabel.Parent := ConfigAdminPage.Surface;

  AddressesUrlText := TNewEdit.Create(ConfigAdminPage);
  AddressesUrlText.Name := 'AddressesUrlText';
  AddressesUrlText.Top := AddressesLabel.Top;
  AddressesUrlText.Left := CertificatesUrlText.Left;
  AddressesUrlText.Width := CertificatesUrlText.Width;
  AddressesUrlText.Parent := ConfigAdminPage.Surface;
  

  DomainsLabel := TNewStaticText.Create(ConfigAdminPage);
  DomainsLabel.Name := 'DomainsLabel'
  DomainsLabel.Caption := 'Domains: ';
  DomainsLabel.Top := AddressesLabel.Top + AddressesLabel.Height + ScaleY(14);
  DomainsLabel.Parent := ConfigAdminPage.Surface;

  DomainsUrlText := TNewEdit.Create(ConfigAdminPage);
  DomainsUrlText.Name := 'DomainsUrlText';
  DomainsUrlText.Top := DomainsLabel.Top;
  DomainsUrlText.Left := CertificatesUrlText.Left;
  DomainsUrlText.Width := CertificatesUrlText.Width;
  DomainsUrlText.Parent := ConfigAdminPage.Surface;


  DnsRecordsLabel := TNewStaticText.Create(ConfigAdminPage);
  DnsRecordsLabel.Name := 'DnsRecordsLabel';
  DnsRecordsLabel.Caption := 'DnsRecords: ';
  DnsRecordsLabel.Top := DomainsLabel.Top + DomainsLabel.Height + ScaleY(14);
  DnsRecordsLabel.Parent := ConfigAdminPage.Surface;

  DnsRecordsUrlText := TNewEdit.Create(ConfigAdminPage);
  DnsRecordsUrlText.Name := 'DnsRecordsUrlText';
  DnsRecordsUrlText.Top := DnsRecordsLabel.Top;
  DnsRecordsUrlText.Left := CertificatesUrlText.Left;
  DnsRecordsUrlText.Width := CertificatesUrlText.Width;
  DnsRecordsUrlText.Parent := ConfigAdminPage.Surface;


  AuthenticationLabel := TNewStaticText.Create(ConfigAdminPage);
  AuthenticationLabel.Name := 'AuthenticationLabel';
  AuthenticationLabel.Caption := 'Authentication: ';
  AuthenticationLabel.Top := DnsRecordsLabel.Top + DnsRecordsLabel.Height + ScaleY(14);
  AuthenticationLabel.Parent := ConfigAdminPage.Surface;

  AuthenticationUrlText := TNewEdit.Create(ConfigAdminPage);
  AuthenticationUrlText.Name := 'AuthenticationUrlText';
  AuthenticationUrlText.Top := AuthenticationLabel.Top;
  AuthenticationUrlText.Left := CertificatesUrlText.Left;
  AuthenticationUrlText.Width := CertificatesUrlText.Width;
  AuthenticationUrlText.Parent := ConfigAdminPage.Surface;


  ConfigAdminPage.OnActivate := @ConfigAdminPageOnActivate;
  ConfigAdminPage.OnNextButtonClick := @SetConfigAdminEndpointsOnClick;
  ConfigAdminPage.OnShouldSkipPage := @ConfigAdminPage_ShouldSkip;

  Result := ConfigAdminPage;
end;


procedure CreateAboutButtonAndURLLabel(ParentForm: TSetupForm; CancelButton: TNewButton);
var    
  URLLabel: TNewStaticText;

begin
  
  URLLabel := TNewStaticText.Create(ParentForm);
  URLLabel.Caption := 'http://wiki.directproject.org/';
  URLLabel.Cursor := crHand;
  URLLabel.OnClick := @URLLabelOnClick;
  URLLabel.Parent := ParentForm;
  { Alter Font *after* setting Parent so the correct defaults are inherited first }
  URLLabel.Font.Style := URLLabel.Font.Style + [fsUnderline];
  URLLabel.Font.Color := clBlue;
  URLLabel.Top := CancelButton.Top + CancelButton.Height - CancelButton.Height - 2;
  URLLabel.Left := ScaleX(20);


end;


//Create Virtual directories
procedure CurStepChanged(CurStep: TSetupStep);
          
begin
  
  //Post install step
  if (CurStep = ssPostInstall) then begin
    //Dns web service somponent selected and Not installing development type
    if (pos( 'dnswebservice', WizardSelectedComponents( false)) > 0) and (pos( 'development', WizardSetupType( false)) = 0) then  
      begin
        CreateIISVirtualDir('DnsService', ExpandConstant('{app}') + '\DnsService', 'Direct DNS Responder Service');
      end; 
    //Config web service component selected and Not installing development type
    if (pos( 'configwebservice', WizardSelectedComponents( false)) > 0)  and (pos( 'development', WizardSetupType( false)) = 0) then  
      begin
        CreateIISVirtualDir('ConfigService', ExpandConstant('{app}') + '\ConfigService', 'Direct Config Service');
      end;
    //Config UI web application component selected and Not installing development type
    if (pos( 'configui', WizardSelectedComponents( false)) > 0)  and (pos( 'development', WizardSetupType( false)) = 0) then  
      begin
        CreateIISVirtualDir('ConfigUI', ExpandConstant('{app}') + '\ConfigUI', 'Direct Config Admin');
      end;
   
  end;
  //Log file maintenance
  if CurStep = ssDone then
    OkToCopyLog := True;


end;



//Remove Virtual directories.
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  DnsWebService_Vdir, ConfigWebService_Vdir, ConfigUiWebApp_Vdir: String;
begin
  
  //Post install step
  if (CurUninstallStep = usUninstall) then begin

    DnsWebService_Vdir := GetIniString('InstallSettings', 'DnsWebService_Vdir', 'unknown', ExpandConstant('{app}') + '\direct.ini');
    ConfigWebService_Vdir := GetIniString('InstallSettings', 'ConfigWebService_Vdir', 'unknown', ExpandConstant('{app}') + '\direct.ini');
    ConfigUiWebApp_Vdir := GetIniString('InstallSettings', 'ConfigUiWebApp_Vdir', 'unknown', ExpandConstant('{app}') + '\direct.ini')


    //Dns web service somponent selected and Not installing development type
    if not (CompareStr('unknown', DnsWebService_Vdir) = 0)  then  
      begin
        DeleteIISVirtualDir(DnsWebService_Vdir);
      end; 
    //Config web service component selected and Not installing development type
    if not (CompareStr('unknown', ConfigWebService_Vdir) = 0)  then  
      begin
        DeleteIISVirtualDir(ConfigWebService_Vdir);
      end;
    //Config UI web application component selected and Not installing development type
    if not (CompareStr('unknown', ConfigUiWebApp_Vdir) = 0)  then  
      begin
        DeleteIISVirtualDir(ConfigUiWebApp_Vdir);
      end;
  end;
end;




procedure InitializeWizard;
var
  Page : TWizardPage;
begin

  Page := CreateDatabaseConnWizardPage;
  Page := CreateGatewayWizardPage(Page);
  Page := CreateGatewaySmtpWizardPage(Page);
  Page := CreateDnsResponderWizardPage(Page); 
  Page := CreateUIConfigWizardPage(Page);

  CreateAboutButtonAndURLLabel(WizardForm, WizardForm.CancelButton);
end;



